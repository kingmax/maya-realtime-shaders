///////////////////////////////////////////////////////////////////////////////////////
//  lcUberShader.cgfx
//
//  Author: Leonardo Covarrubias
//  Date: 03/07/2010
//  Contact: leo.covarrubias@live.com
//  Website: blog.leocov.com - www.leocov.com
//
//  Special thanks for inspiration and learing:
//
//  Ben Cloward - http://www.bencloward.com
//  Brice Vandemoortele - http://www.mentalwarp.com
//  Joel Styles - Fake Subsurface Scattering - www.jistyles.com
//  'The Renderman Sading Language Guide' - Rudy Cortes - www.rslprogramming.com - many basic and shading functions
//  RenderMonkey by ATI - http://developer.amd.com/gpu/rendermonkey/Pages/default.aspx
//  Charles Hollemeersch - litsphere shader - http://charles.hollemeersch.net/
//  'Advanced Game Development with Programmable Graphics Hardware' - Alan Watt, Fabio Policarpo - 1D texture mapping
//  'Uncharted 2: HDR Lighting' presentation by John Hable
//
//  This work is licensed under a  Creative Commons Attribution-Share Alike 3.0 Unported License
//  http://creativecommons.org/licenses/by-sa/3.0/
//
//  Change List:
//
//  1.0 - 03/07/2010 - initial release
//  1.1 - 03/09/2010 - fix some compile errors with typecasting and conditional returns under strict compilers - additional commenting
//  1.2 - 03/10/2010 - added diffuse texture and ambient lighting to toon and litsphere techniques - fixed some typecasting errors on strict compilers.
//  1.3 - 05/25/2010 - small edits to the attributes for Maya 2011 - also added a dummy technique at the end as a temporary fix for Maya 2011 error where the last technique is always selected in the interface
//  1.4 - 07/04/2010 - added baked shadow map option - slight change to glossBlurWeight usage
//  1.5 -            - removed litsphere and toon shaders
//  1.6 - 07/12/2010 - added switch to remove normal maps effect on specular (clearcoat) - added glow only preview technique - added depth preview technique
//  1.7 - 08/19/2010 - alot of cleanup - removed useless flags from user-tweakable values - some cleanup to pixel shader code - shadow map is now 3 lights capable - each light is its own channel r,g,b = 1,2,3 with alpha = AO
//  1.8 - 08/22/2010 - added linear lighting option - sRGB correction for color swatches and color textures
//  1.9 - 08/23/2010 - switched lighting code to an illuminance loop Styles
//  2.0 - 08/27/2010 - completed rewrite
//  2.1 - 10/26/2010 - some bugs squashed - took things out of the light loop that should not have been inside
//  2.2 - 12/14/2010 - changed to 2-pass system for backface lighting - avoids faceforward() edge shading errors
//  2.3 - 01/09/2011 - added masks for cubemap reflection and rimlight - thanks to feedback from Richard Smith
//  2.4 - 01/13/2011 - added point light as 3rd light - added metalic tint to env reflections - made light3 a point light - added slider for alpha clipping value
//  2.5 - 02/08/2011 - revert point light back to directional light
//  2.6 - 02/23/2011 - now suports 3 directional lights and 3 point lights in combination - point lights have attenuation and attenuation multiplier
//  2.7 - 02/24/2011 - switched to gp4fp and gp4vp profiles as they provide support for more math
//  2.8 - 04/21/2011 - some minor changes to code structure, no visual differences
//  2.9 - 08/09/2011 - took out all shadow map code, not worth the expense - testing fp40 vs gp4fp
//  3.0 - 08/22/2011 - fixed problem with specular gloss effect being inverted
//
///////////////////////////////////////////////////////////////////////////////////////

#define MAX_LIGHTS 6

string description = "Leo Covarrubias - blog.leocov.com";
string URL = "http://blog.leocov.com/search/label/cgfx";

////////////////////////////////////////
//     User Tweakables                //
////////////////////////////////////////
float globalTonemap
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.1;
  string UIName   = "Basic Global Tonemapping";
> = 0.0;

float exposure
<
  float UIMin     = -10.0;
  float UIMax     = 10.0;
  float UIStep    = 0.1;
  string UIName   = "Global Exposure";
> = 0.0;

bool linear
<
  string UIName   = "Linear Color Math?";
> = true;

//Toggle diffuse map's alpha channel
bool alphaToggle
<
  string UIName   = "Alpha Toggle (diffuse.a)";
> = true;

float clipPoint
<
  float UIMin     = 0.0;
  float UIMax     = 1.0;
  float UIStep    = 0.01;
  string UIName   = "Alpha Clip Value";
> = 0.5;

//Ambeint
float3 ambientColor
<
  string UIName   = "Ambient Color";
  string Type     = "Color";
> = {0.0, 0.0, 0.0};

//Diffuse
float3 diffuseColor
<
  string UIName   = "Diffuse Color";
  string Type     = "Color";
> = {0.5, 0.5, 0.5};

float3 specularColor
<
  string UIName   = "Specular Color";
  string Type     = "Color";
> = {0.5, 0.5, 0.5};

float specularPower
<
  float UIMin     = 1.0;
  float UIMax     = 256.0;
  float UIStep    = 0.1;
  string UIName   = "Specular Power";
> = 30.0;

float tileTextures
<
  float UIMin     = 1.0;
  float UIMax     = 20.0;
  float UIStep    = 0.1;
  string UIName   = "UV Texture Tiling";
> = 1.0;

bool spacerDiffuse<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Diffuse Textures
bool useDiffuseMap
<
  string UIName   = "Use A Diffuse Texture?";
> = false;

bool useHalfLambert
<
  string UIName   = "Use Half Lambert Diffuse?";
> = false;

texture diffuseMap
<
  string ResourceType = "2D";
  string UIName       = "Diffuse Map";
>;

sampler2D diffuseMapSampler = sampler_state
{
  Texture       = <diffuseMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  MaxAnisotropy = 4;  //some people may need to comment this out
  LODBias       = -0.5;
};

bool spacerSpec<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Specular Additional Attributes
bool useSpecularMap
<
  string UIName = "Use A Specular Map?";
> = false;

bool useGlossAlpha
<
  string UIName = "Use a Gloss Map? (specular.a)";
> = false;

bool useAnisoSpec
<
  string UIName   = "Use Anisotropic Specular?";
> = false;

float anisoAngle
<
  float UIMin     = 0.0;
  float UIMax     = 3.15;
  float UIStep    = 0.1;
  string UIName   = "Anisotropic Angle";
> = 1.0;

bool useGlossySpec
<
  string UIName   = "Use Glossy (Toon) Specular?";
> = false;

float glossySharpness
<
  float UIMin     = -3;
  float UIMax     = 1;
  float UIStep    = 0.1;
  string UIName   = "Glossy Spec Edge Sharpness";
> = 0.85;

float specFresnelExp
<
  float UIMin = 0.0;
  float UIMax = 5.0;
  float UIStep = 0.1;
  string UIName = "Specular Fresnel Exponent";
> = 0.0;

float specFresnelScale
<
  float UIMin = 1.0;
  float UIMax = 15.0;
  float UIStep = 0.1;
  string UIName = "Specular Fresnel Multiplier";
> = 1.0;

texture specularMap
<
  string ResourceType = "2D";
  string UIName       = "Specular Map";
>;

sampler2D specularMapSampler = sampler_state
{
  Texture       = <specularMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  LODBias       = -0.5;
};

bool spacerN<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Normal Map Textures
bool useNormalMap
<
  string UIName   = "Use A Normal Map?";
> = false;

float normalIntensity
<
  float UIMin     = 0.0;
  float UIMax     = 3.0;
  float UIStep    = 0.1;
  string UIName   = "Normal Map Intensity";
> = 1.0;

bool flipGreen
<
  string UIName   = "Invert Normal Map Green Channel?";
> = false;

bool noSpecNormal
<
  string UIName   = "Exclude Specular from Normal Map";
> = false;

//Normalmap Sampler
texture normalMap
<
  string ResourceType = "2D";
  string UIName       = "Normal Map";
>;

sampler2D normalMapSampler = sampler_state
{
  Texture       = <normalMap>;
  MinFilter     = LinearMipMapLinear;
  MagFilter     = Linear;
  WrapS         = Wrap;
  WrapT         = Wrap;
  MaxAnisotropy = 4; //some people may need to comment this out
  LODBias       = -0.5;
};

bool spacerGlow<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Glow Map
bool useGlowTexture
<
  string UIName = "Use A Glow Texture?";
> = false;

float glowIntensity
<
  float UIMin     = 0.0;
  float UIMax     = 10.0;
  float UIStep    = 0.1;
  string UIName   = "Glow Map Intensity";
> = 1.0;

texture glowMap
<
  string ResourceType = "2D";
  string UIName       = "Glow Map";
>;

sampler2D glowMapSampler = sampler_state
{
  Texture   = <glowMap>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapS     = Wrap;
  WrapT     = Wrap;
};

bool spacerRim<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Rim Light
bool useRimLight
<
  string UIName   = "Use Rim Light?";
> = false;

bool useRimLightSec
<
  string UIName   = "Use Secondary Rim Light?";
> = false;

float rimExponent
<
  float UIMin     = 0.0;
  float UIMax     = 5.0;
  float UIStep    = 0.1;
  string UIName   = "Rim Light Exponent";
> = 2.2;

float3 rimColor
<
  string UIName   = "Rim Light Color";
  string Type     = "Color";
> = {0.75, 0.9, 1.0};

float3 rimColorSec
<
  string UIName   = "Rim Light B Color";
  string Type     = "Color";
> = {0.75, 0.9, 1.0};

texture rimMask
<
  string ResourceType = "2D";
  string UIName       = "Rim Light Mask Map";
>;

sampler2D rimMaskSampler = sampler_state
{
  Texture   = <rimMask>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapS     = Wrap;
  WrapT     = Wrap;
};

bool spacerHemi<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Hemispherical Ambient Shading
bool useAmbientHemi
<
  string UIName = "Use Hemispherical Ambient?";
> = false;

float3 skyColor
<
  string UIName   = "Hemispherical Sky Color";
  string Type     = "Color";
> = {0.7, 0.95, 1.0};

float3 groundColor
<
  string UIName   = "Hemispherical Ground Color";
  string Type     = "Color";
> = {0.23, 0.27, 0.12};

bool spacerCube<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////////////////////////////////////////////////
//Cubemap for Reflections and Ambient Shading
bool useAmbCube
<
  string UIName = "Use Cubemap for Ambient?";
> = false;

float ambCubeScale
<
  float UIMin     = 0.0;
  float UIMax     = 50.0;
  float UIStep    = 0.1;
  string UIName   = "Ambient Cubemap Intensity";
> = 1;

bool useReflCube
<
  string UIName = "Use Cubemap for Reflection?";
> = false;

float reflCubeScale
<
  float UIMin     = 0.0;
  float UIMax     = 20.0;
  float UIStep    = 0.1;
  string UIName   = "Reflection Cubemap Intensity";
> = 1;

float reflFresnelExp
<
  float UIMin = 0.0;
  float UIMax = 5;
  float UIStep = 0.1;
  string UIName = "Reflection Fresnel Exponent";
> = 1.0;

float reflCubeGain
<
  float UIMin     = 0.1;
  float UIMax     = 0.9;
  float UIStep    = 0.1;
  string UIName   = "Reflection Cubemap Contrast";
> = 0.5;

float glossBlurWeight
<
  float UIMin = 0.0;
  float UIMax = 1.0;
  float UIStep = 0.1;
  string UIName = "Gloss Blurring Amount";
> = 0.0;

float3 metalicTint
<
  string UIName   = "Metalic Tint Color";
  string Type     = "Color";
> = {1.0, 1.0, 1.0};

texture envCubeMap
<
  string UIName = "Reflection Env Map";
  string ResourceType = "Cube";
>;

samplerCUBE envCubeMapSampler = sampler_state
{
  Texture   = <envCubeMap>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapR     = ClampToEdge;
  WrapS     = ClampToEdge;
  WrapT     = ClampToEdge;
};

texture reflectionMask
<
  string ResourceType = "2D";
  string UIName       = "Cubemap Reflection Mask Map";
>;

sampler2D reflectionMaskSampler = sampler_state
{
  Texture   = <reflectionMask>;
  MinFilter = LinearMipMapLinear;
  MagFilter = Linear;
  WrapS     = Wrap;
  WrapT     = Wrap;
};

bool spacerLights<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
////////////////////////////////////////
//     Lights - position and color    //
////////////////////////////////////////

float4 light1Dir : DIRECTION
<
  string UIName = "Light 1 - Directional Light";
  string Space  = "World";
> = {0.0, -1.0, -1.0, 0.0};

float3 light1Color
<
  string UIName   = "Light1Color";
  string Type     = "Color";
> = {1.0, 1.0, 1.0};

bool spacerLights2<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light2Dir : DIRECTION
<
  string UIName = "Light 2 - Directional Light";
  string Space  = "World";
> = {-1.0, 1.0, 1.0, 0.0};

float3 light2Color
<
  string UIName   = "Light2Color";
  string Type     = "Color";
> = {0.00, 0.00, 0.00};

bool spacerLights3<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light3Dir : DIRECTION
<
  string UIName = "Light 3 - Directional Light";
  string Space  = "World";
> = {-1.0, 1.0, 1.0, 0.0};

float3 light3Color
<
  string UIName   = "Light3Color";
  string Type     = "Color";
> = {0.00, 0.00, 0.00};

bool spacerLights4<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light4Pos : POSITION
<
  string UIName = "Light 4 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light4Color
<
  string UIName   = "Light4Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light4atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 4 Attenuation";
> = 1.0;

bool spacerLights5<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light5Pos : POSITION
<
  string UIName = "Light 5 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light5Color
<
  string UIName   = "Light5Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light5atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 5 Attenuation";
> = 1.0;

bool spacerLights6<string UIName   = ">>>>>>>>>>>>>>>>>>>>>>>>>>";> = false;
float4 light6Pos : POSITION
<
  string UIName = "Light 6 - Point Light";
  string Object = "PointLight";
  string Space  = "World";
> = {0.0, 0.0, 0.0, 0.0};

float3 light6Color
<
  string UIName   = "Light6Color";
  string Type     = "Color";
> = { 0.00, 0.00, 0.00};

float light6atten
<
  float UIMin = 0.0;
  float UIMax = 10000.0;
  float UIStep = 0.1;
  string UIName = "Light 6 Attenuation";
> = 1.0;

////////////////////////////////////////
//     Auto Maxtricies                //
////////////////////////////////////////

float4x4 WorldViewProjection   : WorldViewProjection   ;
float4x4 WorldInverseTranspose : WorldInverseTranspose ;
float4x4 ViewInverse           : ViewInverse           ;
float4x4 World                 : World                 ;

////////////////////////////////////////
//     Structs                        //
////////////////////////////////////////

// input from application
struct app2vert {
  float4 Position : POSITION;
  float2 TexCoord : TEXCOORD0;
  float4 Normal   : NORMAL;
  float4 Binormal : BINORMAL;
  float4 Tangent  : TANGENT;
  float4 VertColor: COLOR0;
};

// output to fragment program
struct vert2pixel {
  float4 hpos          : POSITION;
  float2 UV            : TEXCOORD0;
  float3 worldNormal   : TEXCOORD2;
  float3 worldBinormal : TEXCOORD3;
  float3 worldTangent  : TEXCOORD4;
  float3 eyeVec        : TEXCOORD5;
  float3 vertColor     : TEXCOORD6;
  float3 WSPos         : TEXCOORD7;

};

////////////////////////////////////////
//     VERTEX SHADER                  //
////////////////////////////////////////

vert2pixel VS(app2vert IN, uniform bool backface)
{
  vert2pixel OUT;

  OUT.vertColor = IN.VertColor.rgb;

  OUT.hpos = mul(WorldViewProjection, IN.Position);
  OUT.UV = IN.TexCoord.xy;

  OUT.worldNormal   = (backface) ? -1*mul(WorldInverseTranspose, IN.Normal).xyz : mul(WorldInverseTranspose, IN.Normal).xyz;
  OUT.worldTangent  = mul(WorldInverseTranspose, IN.Tangent).xyz;
  OUT.worldBinormal = mul(WorldInverseTranspose, IN.Binormal).xyz;

  OUT.WSPos = mul(World, IN.Position).xyz;
  OUT.eyeVec.xyz = float3(ViewInverse[0].w,ViewInverse[1].w,ViewInverse[2].w) - OUT.WSPos.xyz;

  return OUT;
}

////////////////////////////////////////
//     Fragment Program Functions     //
////////////////////////////////////////

float3 ungamma22 (float3 input)
{
  return (linear) ? pow(input,2.2) : input;
}

float3 gamma22 (float3 input)
{
  return (linear) ? pow(input,1/2.2) : input;
}

float bias (float val, float b) //Rudy Cortes - The Renderman Shading Language Guide
{
  return (b>0) ? pow(abs(val),log(b) / log(0.5)) : 0;
}

float bias2 (float t, float a) //Rudy Cortes - The Renderman Shading Language Guide - for use with fresnelSchlick
{
    return pow(t,-(log(a)/log(2.0)));
}

float gain (float val, float g) //Rudy Cortes - The Renderman Shading Language Guide
{
  return 0.5 * ((val<0.5) ? bias(2.0*val, 1.0-g) : (2.0 - bias(2.0-2.0*val, 1.0-g)));
}

float3 filmicTonemap(float3 input) //John Hable's filmic tonemap function with fixed values
{
  float A = 0.22;
  float B = 0.3;
  float C = 0.1;
  float D = 0.2;
  float E = 0.01;
  float F = 0.3;
  float linearWhite = 11.2;
  float3 Fcolor = ((input*(A*input+C*B)+D*E)/(input*(A*input+B)+D*F)) - E/F;
  float  Fwhite = ((linearWhite*(A*linearWhite+C*B)+D*E)/(linearWhite*(A*linearWhite+B)+D*F)) - E/F;
  return Fcolor/Fwhite;
}

float attenuation (float3 L, float scale)
{
    float d = length(L);
    return (1/d)*scale;
}

float fresnel (float3 N, float3 V, float fresnelExp) //this is the most basic approximation of the fresnel function
{
    return max(0.f,pow(abs(1.0-dot(N,V)),fresnelExp));
}

float3 lambert (float3 L, float3 N)
{
  return float3( max(0.f,dot(N,L)) );
}

float3 halfLambert (float3 L, float3 N) //Valve's Wrapped Diffuse Function
{
    return max(0.f,pow(dot(N,L)*0.5+0.5, 2));
}

float3 blinn (float3 L, float3 V, float3 N, float roughness)
{
  float3 H = normalize(L + V);
  float NdH = max(0.f,dot(N,H));
  return smoothstep(-0.1,0.1,dot(N,L)) * pow(NdH, roughness);
}

float3 glossy (float3 L, float3 V, float3 N, float roughness, float sharpness)
//glossy ceramic like specular highlight - not physically accurate - sourced from The Renderman Shading Language Guide
//values 0.18, 0.72 are arrived at empirically - roughness/3.333 brings the size into an equivalent range as blinn()
{
  float w = 0.18 * (1-sharpness);
  float3 H = normalize(L + V);
  float NdH = dot(N,H);
  return smoothstep(-0.1,0.1,dot(N,L)) * smoothstep(0.72-w,0.72+w,pow(max(0.f,NdH),roughness/3.333));
}

//adapted from anisotropic.fx from ati rendermonkey example file - based on Steve Westin Brushed Metal work
float3 anisotropic (float3 normalTexture, float3 L, float3 V, float3 N, float3 Bn, float3 Tn, float roughness, float angle)
{
  if(flipGreen)normalTexture.g = 1.0 - normalTexture.g;
  float cosA, sinA;
  sincos(angle, sinA, cosA); //gives back the sin of angle and cos of angle into sinA and cosA variables - faster than doing each seperatly
  float3 Ta = sinA*Tn + cosA*Bn;
  if(useNormalMap) Ta = sinA*(Tn*normalTexture.r) + cosA*(Bn*normalTexture.g); //vary the world normal and binormal by the texture normalmap x and y - but dont remap the normalmap to -1,1  keep as 0,1 - dont normalize it will negate the effect of multiply by scalar
  float cs = -dot(V, Ta);
  float sn = sqrt(1 - cs * cs);
  float cl = dot(L, Ta);
  float sl = sqrt(1 - cl * cl);

  if (useHalfLambert) return smoothstep(0,0.5, pow(dot(N,L)*0.5+0.5, 2) ) * (pow(max(0.f,cs*cl + sn*sl), roughness));

  return smoothstep(0,0.1,dot(N,L)) * (pow(max(0.f,cs*cl + sn*sl), roughness));
}

//this returns a float for mipmap level selection
float glossBlur (float specPow, float glossTex, float weight)
{
  float mipMax = 7;
  specPow = (specPow>=1) ? 1 : specPow;
  float G = (specPow*glossTex);
  return mipMax*G*weight;
}

//////////////////////////////////////////////////
//     Compartmentalized Shading Components     //
//////////////////////////////////////////////////

////////////////////////////////////////
// Tangent Space Normal Mapping
float3 normalsTangent (float4 normalTexture,
                       float3 Nn,
                       float3 Bn,
                       float3 Tn,
                       float3 V)
{
  if(flipGreen)normalTexture.g = 1.0 - normalTexture.g;

//  if(backFaceLighting == true) Nn = faceforward(Nn,-V,Nn);
  if(useNormalMap)
  {
    normalTexture.rgb = normalTexture.rgb * 2.0 - 1.0;
    normalTexture.xy *= normalIntensity;
    if (normalIntensity > 0) Nn = normalize((normalTexture.x*Tn)+(normalTexture.y*Bn)+(normalTexture.z*Nn));
  }

  return Nn;
}

////////////////////////////////////////
// Ambient Shading - Uniform, Hemisphere, and Cubemap
float3 ambientEnv (float3 N)
{
  groundColor = ungamma22(groundColor);
  skyColor = ungamma22(skyColor);

  if(useAmbientHemi) return N.y * (0.5*(skyColor-groundColor)) + (0.5*(skyColor+groundColor)); //simplification thanks to Greg Seegert //lerp(groundColor,skyColor,max(0.f,0.5*N.y+0.5));  //linearly interpolate between the two colors based on the World Normal's Y/up vector (shifted into 0-1 range)
  if(useAmbCube) return ungamma22(texCUBElod(envCubeMapSampler, float4(N,7.8)).rgb)*ambCubeScale;
  return 1;
}

////////////////////////////////////////
// Rim lighting effect
float3 rimLightDual (float3 diffuseComp,
                    float3 V,
                    float3 Nn)
{
  rimColor = ungamma22(rimColor);
  rimColorSec = ungamma22(rimColorSec);
  float Fr = fresnel(Nn,V,rimExponent);
  float3 Rim = 0;
  if(useRimLight)    Rim += Fr*rimColor*max(0.f,pow(dot(Nn,float3(0,1,0))*0.5+.5,2));//I use a half lambert style calculation on N.Y instead of standard lambert because it gives a softer blend across the surface
  if(useRimLightSec) Rim += Fr*rimColorSec*(1.0-diffuseComp);
  return Rim;
}

////////////////////////////////////////
// CubeMap Reflection
float3 reflectionCube (float4 specularTexture,
                       float3 N,
                       float3 V)
{
  float3 REnv = 0;
  float3 R = reflect(-V,N); //reflection vector
  float roughness = (useGlossAlpha) ? specularTexture.a : 1;
  float Fr = fresnel(N,V,reflFresnelExp);
  float miplvl = glossBlur (specularPower, roughness, glossBlurWeight); //set a mipmap level to give the impression of blur
  if(useReflCube) REnv = ungamma22(texCUBElod(envCubeMapSampler, float4(R,miplvl)).rgb);  //lets you choose a mip level for the cube map to make it appear blurred

  //apply a contrast function to increase or decrease contrast - most useful as an increase
  REnv.r = gain(REnv.r,reflCubeGain);
  REnv.g = gain(REnv.g,reflCubeGain);
  REnv.b = gain(REnv.b,reflCubeGain);

  return REnv*reflCubeScale*Fr;
}

////////////////////////////////////////
// Glow Texture / Incandescence
float3 glow (float3 glowTexture)
{
  float3 Glow = 0;
  if(useGlowTexture) Glow = glowTexture*glowIntensity;
  return Glow;
}

////////////////////////////////////////
// Alpha Texture
float alpha (float4 diffuseTexture)
{
  float A = 1;
  if(alphaToggle) A = diffuseTexture.a;

  return A;
}

////////////////////////////////////////
//     Pixel Shaders                  //
////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////
//     Normals Only
//////////////////////////////////////////////////////////////////////////////////////////////
float4 PSNormals (vert2pixel IN) : COLOR //visualize the normals in worldspace
{
  float2 UV = IN.UV*tileTextures;
  //////////////////////
  // Normal Component //
  float3 N = normalsTangent(tex2D(normalMapSampler, UV),
                            normalize(IN.worldNormal),
                            normalize(IN.worldBinormal),
                            normalize(IN.worldTangent),
                            normalize(IN.eyeVec.xyz));
  N = 0.5 * N + 0.5;  //I need to shift the normals into the 0-1 range for viewing as color
                      //otherwise they are in the -1 to 1 range
  /////////////////////
  // Alpha Component //
  float Alpha = alpha(tex2D(diffuseMapSampler, IN.UV.xy));

  return float4(N,Alpha);
}

//////////////////////////////////////////////////////////////////////////////////////////////
//     Diffuse Only
//////////////////////////////////////////////////////////////////////////////////////////////
float4 PSDiffuse (vert2pixel IN) : COLOR
{
  float2 UV   = IN.UV*tileTextures;

  float4 Dt   = tex2D(diffuseMapSampler, UV);
  Dt.rgb = ungamma22(Dt.rgb);
  float4 Gt   = tex2D(glowMapSampler, UV);
  Gt.rgb = ungamma22(Gt.rgb);

  diffuseColor  = ungamma22(diffuseColor);

  float3 Ci = 0;  //Incident Color

  Ci += (useDiffuseMap) ? Dt.rgb : diffuseColor;

  //Glow Component
  Ci += glow(Gt.rgb);

  // Alpha Component
  float Oi = alpha(Dt);

  Ci = lerp(gamma22(Ci*pow(2,exposure)),gamma22(filmicTonemap(Ci*pow(2,exposure))),globalTonemap);

  return float4(Ci,Oi);  //Incident Color and Incident Opacity
}

//////////////////////////////////////////////////////////////////////////////////////////////
//     Full Shading
//////////////////////////////////////////////////////////////////////////////////////////////

float4 PSFull (vert2pixel IN) : COLOR
{
  float3 lights [MAX_LIGHTS][3]= { { (-light1Dir.xyz),       light1Color, 0.0 },
                                   { (-light2Dir.xyz),       light2Color, 0.0 },
                                   { (-light3Dir.xyz),       light3Color, 0.0 },
                                   { (light4Pos.xyz - IN.WSPos), light4Color, light4atten },
                                   { (light5Pos.xyz - IN.WSPos), light5Color, light5atten },
                                   { (light6Pos.xyz - IN.WSPos), light6Color, light6atten }
                                 };

  float3 V   = normalize(IN.eyeVec);
  float3 Nw  = normalize(IN.worldNormal);
  float3 Bw  = normalize(IN.worldBinormal);
  float3 Tw  = normalize(IN.worldTangent);

  float2 UV   = IN.UV*tileTextures;

  // Texture Components
  float4 Nt   = tex2D(normalMapSampler, UV);
  float4 Dt   = tex2D(diffuseMapSampler, UV);
  Dt.rgb = ungamma22(Dt.rgb);
  float4 St   = tex2D(specularMapSampler, UV);
  St.rgb = ungamma22(St.rgb);
  float4 Gt   = tex2D(glowMapSampler, UV);
  Gt.rgb = ungamma22(Gt.rgb);
  float3 Rimt = tex2D(rimMaskSampler, UV).rgb;
  float3 Reflt = tex2D(reflectionMaskSampler, UV).rgb;

  ambientColor  = ungamma22(ambientColor);
  diffuseColor  = ungamma22(diffuseColor);
  specularColor = ungamma22(specularColor);

  // Normal Component
  float3 N = normalsTangent(Nt, Nw, Bw, Tw, V);

  // Light Loop
  // Diffuse and Specular
  float3 Ci = 0;  //Incident Color
  float3 Cd = 0;  //Diffuse of lights only, no textures

  float FrSpec = fresnel(N,V,specFresnelExp)*specFresnelScale;

  if(useGlossAlpha) specularPower*=pow(abs(St.a-2),2);  //here i take the aproach of increasing the specularPower by the alpha instead of simply multiplying and reducing specPower - this avoids issues when specPower aproaches Zero - using pow() is arbitrary but creates a visualy pleasing distinction between sizes
  if(useSpecularMap) specularColor*=St.rgb;
  if(specularPower<1)specularColor=0; //avoids problems with small specularPower values

  for ( int i = 0; i < MAX_LIGHTS; i++  ) //Illuminance loop
  {
    float3 L = normalize(lights[i][0]);
    float3 Lc = ungamma22(lights[i][1]); //light color is an interface color swatch and needs to be gamma corrected
    if(i>2) Lc*=attenuation(lights[i][0], lights[i][2]);//La;

    float3 Diffuse  = lambert(L, N);
    if (useHalfLambert) Diffuse = halfLambert(L, N);

    //new Normal and Normalmap variables allow seperating normalmaps on specular vs diffuse and other effects
    float3 specN = N;
    float3 specNt = Nt.rgb;
    if (noSpecNormal){
      specN = Nw;
      specNt = float3(0.5,0.5,1);
    }
    float3 Specular = blinn(L, V, specN, specularPower);
    if(useAnisoSpec)  Specular = anisotropic(specNt, L, V, specN, Bw, Tw, specularPower, anisoAngle);
    if(useGlossySpec) Specular = glossy (L, V, specN, specularPower, glossySharpness);

    Diffuse *= Lc;
    Cd += Diffuse; //store a variable of just the diffuse light intensity/colors
    Diffuse *= (useDiffuseMap) ? Dt.rgb : diffuseColor;

    Specular *= Lc* FrSpec * specularColor;

    Ci += (Diffuse+Specular);
  }

  // Rim Light Component
  Ci += rimLightDual (Cd,  //use Cd instead of Ci to mask the rim light by only the diffuse light intensity
                      V,
                      N) * Rimt;

  // Cubemap Reflection Component
  Ci += reflectionCube(St,
                       N,
                       V) * Reflt * metalicTint;

  //Ambient Component
  ambientColor *= ambientEnv(N);
  Ci += (useDiffuseMap) ? ambientColor*Dt.rgb : ambientColor*diffuseColor;

  //Glow Component
  Ci += glow(Gt.rgb);

  // Alpha Component
  float Oi = alpha(Dt);

  Ci = lerp(gamma22(Ci*pow(2,exposure)),gamma22(filmicTonemap(Ci*pow(2,exposure))),globalTonemap);

  return float4(Ci,Oi);  //Incident Color and Incident Opacity
}

////////////////////////////////////////
//     Techniques                     //
////////////////////////////////////////

technique Full_1_Bit_Alpha //onebit alpha testing
{
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    AlphaTestEnable = true;           //alpha test flag
    AlphaFunc = float2(Greater, clipPoint);  //alpha test flag
    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSFull();
  }
}

technique Full_1_Bit_Alpha_2_Sided //onebit alpha testing
{
  pass Back
  {
    CullFaceEnable=true;
    CullMode = Front;
    AlphaTestEnable = true;           //alpha test flag
    AlphaFunc = float2(Greater, clipPoint);  //alpha test flag
    VertexProgram   = compile vp40 VS(true);
    FragmentProgram = compile fp40 PSFull();
  }
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    AlphaTestEnable = true;           //alpha test flag
    AlphaFunc = float2(Greater, clipPoint);  //alpha test flag
    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSFull();
  }
}

technique Full_8_Bit_Alpha //8bit alpha blending - has problems in maya viewport with sorting
{
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    
    DepthTestEnable=true;
    DepthMask = true;
    DepthFunc = LEqual;
    
    BlendEnable = true;
    BlendFunc = int2( SrcAlpha, OneMinusSrcAlpha);

    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSFull();
  }
}

technique Full_8_Bit_Alpha_2_Sided //8bit alpha blending - has problems in maya viewport with sorting
{
  pass Back
  {
    CullFaceEnable=true;
    CullMode = Front;
    
    DepthMask = true;
    DepthTestEnable = true;
    DepthFunc = LEqual;
    
    ZEnable = true;
    ZMWriteEnable = true
    ZFunc = LEqual;
    
    BlendEnable = true;                             //alpha blending flag
    BlendFunc = float2(srcalpha, oneminussrcalpha); //alpha blending flag
    VertexProgram   = compile vp40 VS(true);
    FragmentProgram = compile fp40 PSFull();
  }
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    
    DepthTestEnable=true;
    DepthMask = true;
    DepthFunc = LEqual;
    
    BlendEnable = true;                             //alpha blending flag
    BlendFunc = float2(srcalpha, oneminussrcalpha); //alpha blending flag
    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSFull();
  }
}

technique Diffuse_1_Bit_Alpha //onebit alpha testing
{
  pass Both
  {
    CullFaceEnable=false;
    AlphaTestEnable = true;           //alpha test flag
    AlphaFunc = float2(Greater, clipPoint);  //alpha test flag
    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSDiffuse();
  }
}

technique Diffuse_8_Bit_Alpha //8bit alpha blending - has problems in maya viewport with sorting
{
  pass Both
  {
    CullFaceEnable=false;
    BlendEnable = true;                             //alpha blending flag
    BlendFunc = float2(srcalpha, oneminussrcalpha); //alpha blending flag
    VertexProgram   = compile vp40 VS(false);
    FragmentProgram = compile fp40 PSDiffuse();
  }
}

technique World_Normals
{
  pass Back
  {
    CullFaceEnable=true;
    CullMode = Front;
    VertexProgram   = compile arbvp1 VS(true);
    FragmentProgram = compile arbfp1 PSNormals();
  }
  pass Front
  {
    CullFaceEnable=true;
    CullMode = Back;
    VertexProgram   = compile arbvp1 VS(false);
    FragmentProgram = compile arbfp1 PSNormals();
  }
}

technique _ //empty technique for Maya 2011
{
  pass P0
  {
    //empty
  }
}